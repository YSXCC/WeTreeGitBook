# 具体代码实现

## pm.inc文件

```x86asm
;定义多行宏，%macro  宏的命名  参数个数
;%1表示第一个参数   %2表示第二个参数    %3表示第三个参数
;初始化段描述寄存器
%macro Descriptor 3
    dw    %2  &  0FFFFh                         ;段界限1
    dw    %1  &  0FFFFh                         ;段基址1
    db   (%1 >> 16) & 0FFh                      ;段基址2
    dw   ((%2 >> 8) & 0F00h) | (%3 & 0F0FFh)    ;属性1 + 段界限2 + 属性2
    db   (%1 >> 24) & 0FFh                      ;段基址3
%endmacro


DA_32   EQU  4000h  ; 32 位段                       0100 0000 0000 0000
DA_C    EQU  98h    ; 存在内存的只执行代码段属性值                1001 1000
DA_DRW  EQU  92h    ; 存在内存的可读写数据段属性值                1001 0010

```

## boot.asm文件

```x86asm
;编译方法
;nasm boot.asm -o boot.bin
;之后将boot.bin写入软盘中
%include "pm.inc"

org 0x7c00  ;加载程序到内存-----7c00-----处,ORG是伪指令
    jmp LABEL_BEGIN

;GDT定义
[SECTION .gdt]
;GDT属性                        段基址       段界限       段属性
LABEL_GDT:         Descriptor     0,          0,          0            ;空描述符
LABEL_DESC_CODE32: Descriptor     0,    SegCode32Len-1, DA_C + DA_32   ;非一致代码段
LABEL_DESC_VIDEO:  Descriptor   0B8000h,    0ffffh,     DA_DRW         ;显存首地址
;GDT结束

GdtLen      equ    $  -  LABEL_GDT      ;GDT长度
GdtPtr      dw     GdtLen - 1           ;GDT界限  
            dd     0                    ;GDT基地址

;GDT选择子
SelectorCode32     equ      LABEL_DESC_CODE32   -   LABEL_GDT
SelectorVideo      equ      LABEL_DESC_VIDEO    -   LABEL_GDT
;END of [SECTION .gdt]

[SECTION .s16]
[BITS 16]
LABEL_BEGIN:
    mov ax, cs  ;初始化代码段、数据段和堆栈段
    mov ds, ax
    mov es, ax
    mov ss, ax
    mov sp, 0100h

;初始化32位代码段描述符
    xor eax, eax
    mov ax, cs
    shl eax, 4                              ;寻址方式是 16*cs+ip,所以要乘以4
    add eax, LABEL_SEG_CODE32               ;获取32位代码段的基址
    mov word [LABEL_DESC_CODE32 + 2], ax    ;段基址1放入描述符中
    shr eax, 16
    mov byte [LABEL_DESC_CODE32 + 4], al    ;段基址2放入描述符中
    mov byte [LABEL_DESC_CODE32 + 7], ah    ;段基址3放入描述符中

;为加载GDTR做准备
    xor eax, eax
    mov ax, ds
    shl eax, 4
    add eax, LABEL_GDT                      ;获得GDT基地址
    mov dword [GdtPtr + 2], eax             ;放在GdtPtr的GDT基地址

;加载GDTR
    lgdt    [GdtPtr]

;关中断
    cli

;打开地址线A20
    in al, 92h
    or al, 00000010b
    out 92h, al

;准备切换保护模式
    mov eax, cr0
    or eax, 1
    mov cr0,eax

;真正进入保护模式
    jmp dword SelectorCode32:0

;END of [SECTION .s16]

[SECTION .s32]
[BITS 32]
LABEL_SEG_CODE32:
    mov ax, SelectorVideo
    mov gs, ax
    mov si, Msg
    mov ebx, 10
    mov ecx, 2

    mov edi, (80*11+25) * 2     ;总共占两个字节(80x25个字符)
ShowStr:                        ;前一个字节为ASCII码，后一个为显示属性）
    mov ah, 0CH                 ;0000黑底   1100红字
    mov al, [si]                ;ASCII码值
    cmp al, 0
    je  END

    mov [gs:edi], ax
    add edi, 2
    add si, 1
    jmp ShowStr

;到此为止
END:
    jmp $

Msg:
    DB  "In Protected Mode" , 0

SegCode32Len    equ     $ - LABEL_SEG_CODE32
```

## 因为要保证引导扇区为512字节，所以两种方式

- 1、可以自己写一个写盘工具（所以自己用java写了一个工具，帮助写入）

- ~~2、借用别人的引导扇区~~（没有找到对应的方法）

## 启动图像

![WeTreeOS启动](https://gitee.com/YSXCC/MDImage/raw/master/img/20200210171048.png)
