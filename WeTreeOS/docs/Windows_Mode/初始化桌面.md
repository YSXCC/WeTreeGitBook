# 初始化桌面

## 代码实现

---

* 在编译完之后，发现多了几个扇区，但是BIOS只读了第一个扇区，所以就需要调用13号中断读盘操作。（那个BIOS操作手册有部分存在问题，特别是那个显示模式的分辨率）

* 缓冲区就是为了保证跳转之后有代码可以执行，这个ES：BX是BIOS规定的

```x86asm
org 0x7c00  ;加载程序到内存-----7c00-----处,ORG是伪指令

LOAD_ADDR  EQU  08000h  ;内核地址

readFloppy:
    mov CH, 1         ;CH 用来存储柱面号
    mov DH, 0         ;DH 用来存储磁头号
    mov CL, 1         ;CL 用来存储扇区号

    mov BX, LOAD_ADDR ;ES:BX 数据存储缓冲区

    mov AH, 0x02      ;AH = 02 表示要做的是读盘操作
    mov AL, 10        ;AL 表示要连续读取几个扇区
    mov DL, 0         ;驱动器编号，一般我们只有一个软盘驱动器，所以写死为0
    INT 0x13          ;调用BIOS中断实现磁盘读取功能

    JC fin            ;CF = 1时候跳转，当CF = 0表示读盘成功

    jmp LOAD_ADDR

fin:
    HLT
    jmp fin
```

* 最先修改的是描述符，那么这里就巨坑，LABEL_DESC_VRAM设不设置都没多大关系，关键是LABEL_DESC_DATA必须要有，具体原因不是很清楚，可能是后面C语言编译之后的代码需要较长的段存储

```x86asm
LABEL_DESC_VRAM:   Descriptor   0A0000h,    0ffffh,     DA_DRW         ;图像模式显存地址
LABEL_DESC_DATA:   Descriptor      0,     0ffffffffh,   DA_DRW         ;数据段

SelectorVram       equ      LABEL_DESC_VRAM     -   LABEL_GDT
SelectorData       equ      LABEL_DESC_DATA     -   LABEL_GDT
```

* 之后就可以切换到图像模式，记住这是在16位模式下做的事情，因为32位下不能调用BIOS中断。

```x86asm
;切换显示器模式
    mov al, 0x13
    mov ah, 0
    int 0x10
```

* 代码段需要把显存的地址初始化，有点扯，看起来好像不受GDT的约束，注意VRAM_ADDRESS是声明在最前的一块地址，之后C语言调用的时候，只是取这个地址上的值，而不是这个值的本身。

```x86asm
LABEL_SEG_CODE32:
    mov dword ebx,0x0A0000          ;在这里初始化显存地址,在C语言调用
    mov dword [VRAM_ADDRESS],ebx

    mov ax, SelectorData
    mov ds, ax
%include "ckernel.asm"
```

* 这里实在汇编中定义的一些函数，因为在C语言中需要操纵端口进行编程。

* 需要特别注意的是端口的输入这些函数

```x86asm
io_hlt:   ;void io_hlt(void);暂停
    HLT
    RET

io_cli:   ;void io_cli(void);关中断
    CLI
    RET

io_sti:   ;开中断
    STI
    RET

io_stihlt: ;开中断后暂停
    STI
    HLT
    RET

;端口输出
io_out8:                    ;EDX保存端口号，EAX保存数据
    mov edx, [esp + 4]      ;void io_out(int port, int data)
    mov al,  [esp + 8]      ;根据规定，从右边向左压栈，
    out dx, al              ;先进栈的是数据data，32位，之后是端口号，也是32位（int）
    ret                     ;al输出8位，ax输出16位，eax输出32位

io_out16:
    mov edx, [esp + 4]
    mov ax,  [esp + 8]
    out dx, ax
    ret

io_out32:
    mov edx, [esp + 4]
    mov eax, [esp + 8]
    out dx, eax
    ret

io_load_eflags:           ;保存现场
    pushfd
    pop  eax
    ret

io_store_eflags:          ;恢复现场
    mov eax, [esp + 4]
    push eax
    popfd
    ret
```

* 初始化调色板，就是把颜色的RGB十六进制输入到相应端口

```c
void init_palette(void) {
    static  unsigned char table_rgb[16 *3] = {
        0x00,  0x00,  0x00,     //全黑
        0xff,  0x00,  0x00,     //纯红
        0x44,  0x8c,  0xbb,     //不知道，但是看起来很舒服
        0xff,  0xff,  0x00,     //亮黄
        0x00,  0x00,  0xff,     //亮蓝
        0xff,  0x00,  0xff,     //亮紫
        0x00,  0xff,  0xff,     //浅亮
        0xff,  0xff,  0xff,     //全白
        0xc6,  0xc6,  0xc6,     //亮灰
        0x84,  0x00,  0x00,     //暗红
        0x00,  0x84,  0x00,     //暗绿
        0x84,  0x84,  0x00,     //暗黄
        0x00,  0x00,  0x84,     //暗蓝
        0x84,  0x00,  0x84,     //暗紫
        0x00,  0x84,  0x84,     //浅暗蓝
        0x84,  0x84,  0x84,     //暗灰
    };
    set_palette(0, 15, table_rgb);
    return ;
}

void set_palette(int start, int end, unsigned char *rgb){
    int i, eflages;
    eflages = io_load_eflags();
    io_cli();
    io_out8(0x03c8,start);              //调色板编号
    for(i = start; i <= end ; i++){
        io_out8(0x03c9, rgb[0]/4);      //VGA显卡还支持不了这么多颜色，只能4个一跳
        io_out8(0x03c9, rgb[1]/4);
        io_out8(0x03c9, rgb[2]/4);

        rgb += 3;
    }
    io_store_eflags(eflages);
    return  ;
}
```

## 效果

![桌面](https://gitee.com/YSXCC/MDImage/raw/master/img/20200211215355.png)
